DEFINE_ACTION_MACRO InstallSpecificContents
BEGIN		
	APPEND ~MASTAREA.2DA~
~DD2300   value
DD1800   value
DD2500   value
DD1200   value
DX2001   value~
	UNLESS ~DD2300~
	
	COPY_EXISTING ~DD0334.are~ 		~override~ WRITE_ASCII 0x94 ~DD0334~ #8
	
	ACTION_IF GAME_IS ~bg2ee eet~ BEGIN	
		COPY_EXISTING ~ICEGOLEM.cre~	~override~ WRITE_LONG  0x28 0xE0F0 // GOLEM_ICE/IC_ICE_GOLEM
		COPY_EXISTING ~FROST01.cre~ 	~override~ WRITE_LONG  0x28 0xE24F // IC_GIANT_FROST
		COPY_EXISTING ~DDFROST1.cre~ 	~override~ WRITE_LONG  0x28 0xE24F // IC_GIANT_FROST
	END
	ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN
		COPY_EXISTING ~FROST01.cre~ 	~override~ WRITE_LONG  0x28 0xE710 // IC_OROG2
		COPY_EXISTING ~DDFROST1.cre~ 	~override~ WRITE_LONG  0x28 0xE710 // IC_OROG2
	END		
		
//	LAM HandleSpellScrollsContract	
//	LAM HandleItemsContract	
	
	EXTEND_TOP ~AR0020.bcs~ ~%workspace%/%COMP_FOLDER%/Snip/tAR0020.baf~

// EE Version	
	// Barheal.sto gets medium wounds heal
	COPY_EXISTING ~Barheal.sto~ ~override~
		LPF ADD_STORE_CURE
			INT_VAR price = 150
			STR_VAR spell_name = ~SPPR315~ position = ~AFTER SPPR103~
		END

	COPY_EXISTING ~DD0903.are~ ~override~
		GET_OFFSET_ARRAY trigger_array ARE_V10_REGIONS  
		PHP_EACH trigger_array AS int => trigger_offset BEGIN
			READ_ASCII trigger_offset trigger_name (32) NULL
			PATCH_IF (~%trigger_name%~ STR_EQ ~Tran0900~) BEGIN
				WRITE_ASCII trigger_offset ~Tra0900b~
			END
		END
	
	COPY_EXISTING ~DD4003.are~ ~override~
		LPF REPLACE_AREA_ITEM
			STR_VAR
				old_item = ~ARDEADX~ new_item = ~SCRL75~
		END
		LPF REPLACE_AREA_ITEM
			STR_VAR
				old_item = ~SCRX41~ new_item = ~DDDDBOOK~
		END
// end EE Version	
	
	OUTER_TEXT_SPRINT sender ~%COMP_FOLDER%~
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~ LAF Accept STR_VAR sender target = ~_ESPURTA~ END
END


