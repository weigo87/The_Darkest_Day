//BACKUP ~TDD/backup~
BACKUP ~weidu_external/backup/TDD~
//AUTHOR ~shadowlich~
SUPPORT ~https://forums.beamdog.com/discussion/4841/the-darkest-day/~
VERSION ~v2.0 alpha~

ASK_EVERY_COMPONENT

ALWAYS
	ACTION_IF GAME_IS ~bgt soa tob~ BEGIN
        OUTER_SPRINT engine ~non-ee~
    END 
	ACTION_IF GAME_IS ~eet bg2ee~ BEGIN
        OUTER_SPRINT engine ~ee~
    END
	
	OUTER_SPRINT workspace "weidu_external/workspace/%MOD_FOLDER%"
	MKDIR "%workspace%"
	
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~
	INCLUDE ~%MOD_FOLDER%/Lib/a7_tools.tpa~
	INCLUDE ~%MOD_FOLDER%/Lib/a7_pvrz_tis.tpa~
	INCLUDE ~%MOD_FOLDER%/Lib/Areas.tpa~
	INCLUDE ~%MOD_FOLDER%/Lib/Importer.tpa~

	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END

ACTION_IF NOT VARIABLE_IS_SET one_time_execution THEN BEGIN // check to make this happen only once per install
    OUTER_SET one_time_execution = 1

    // define variable for HANDLE_CHARSETS out_path so it can be reused later
    OUTER_TEXT_SPRINT "HANDLE_CHARSETS_OUT_PATH" EVAL "weidu_external/Language/%MOD_FOLDER%"
    // do not convert Setup.tra files, those should be UTF8 already
    ACTION_DEFINE_ARRAY charsetsNoConvertArray BEGIN tp2 setup END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
    ACTION_DEFINE_ARRAY charsetsReloadArray BEGIN END // List of tra files that need to be reloaded after conversion because they were previously loaded in the LANGUAGE section
    // On EE games, convert game content tra files to utf-8 so that games don't crash when encountering international characters.
    LAF HANDLE_CHARSETS
        INT_VAR
            from_utf8 = 1
            infer_charsets = 1
            verbose = 1 // you can disable this if you want to skip verbose output
        STR_VAR
            default_language = ~English~
            tra_path = EVAL "%MOD_FOLDER%/language"
            out_path = EVAL "%HANDLE_CHARSETS_OUT_PATH%" // location of converted .tra files
            iconv_path = EVAL "%MOD_FOLDER%/Tools/iconv" //available as part of the base system on OS X and GNU/Linux
            reload_array = charsetsReloadArray
            noconvert_array = charsetsNoConvertArray
    END
	
    // for AUTO_TRA: if the game is the enhanced edition, the conversion will not happen so all .tra files should be loaded from "%MOD_FOLDER%/Language"
    ACTION_IF (GAME_IS "bgee bg2ee eet iwdee pstee") THEN BEGIN
		OUTER_TEXT_SPRINT "TRA_LOCATION" EVAL "%HANDLE_CHARSETS_OUT_PATH%"
		COPY ~%MOD_FOLDER%/language/%LANGUAGE%~ ~%TRA_LOCATION%/%LANGUAGE%~
    END

    // for AUTO_TRA: if the game is the classic edition, all converted .tra files will be loaded from HANDLE_CHARSETS out_path
    ACTION_IF NOT (GAME_IS "bgee bg2ee eet iwdee pstee") THEN BEGIN
        OUTER_TEXT_SPRINT "TRA_LOCATION" EVAL "%HANDLE_CHARSETS_OUT_PATH%"
    END
    // load ee-like item desctiptions
    ACTION_IF GAME_IS ~bgee bg2ee eet iwdee pstee~ THEN BEGIN
        LOAD_TRA ~%MOD_FOLDER%/Language/%LANGUAGE%/ITEMS-EE.tra~
    END
  END
END


// for the EE games, the variable will point to the untouched .tra files
// for classic games will point to the HANDLE_CHARSETS out_path value where converted files are stored
AUTO_TRA "%TRA_LOCATION%/%s"



LANGUAGE 	~English~
			~English~
			~TDD/Language/English/Setup.tra~
			~TDD/Language/English/tp2.tra~

LANGUAGE 	~Russian(aerie-ru)~
			~Russian~
			~TDD/Language/Russian/setup.tra~
			~TDD/Language/Russian/tp2.tra~

LANGUAGE 	~Polish(by SoM, part of CoB Clan, in co-operation with TB Team)~
			~Polish~
			~TDD/Language/Polish/setup.tra~
			~TDD/Language/Polish/tp2.tra~

LANGUAGE 	~German(Leonardo Watson)~
			~German~
			~TDD/Language/German/setup.tra~
			~TDD/Language/German/tp2.tra~

LANGUAGE 	~Francais(Isaya)~
			~French~
			~TDD/Language/french/setup.tra~
			~TDD/Language/french/tp2.tra~

LANGUAGE 	~Italian(ilot)~
			~Italian~
			~TDD/Language/Italian/setup.tra~
			~TDD/Language/Italian/tp2.tra~

LANGUAGE 	~Spanish(Clan REO)~
			~Spanish~
			~TDD/Language/Spanish/setup.tra~
			~TDD/Language/Spanish/tp2.tra~

// TODO: TTPOT.itm has weird name in vanilla BG2EE...is it normal???


// Notes:
// - INITDLG.d @override!
// - DDTROLL1.BAF makes trolls killable
// - Is GTRSK002 useless? ex: LeaveAreaLUA("dd9007","GTRSK002",[381.331],12)


// TODO 
// Force add empty area.bcs
// Re-check EE minimap

// DDGAME03.baf SUMMON_SKELETON_WARRIORS
// Level 50 Rules MXSPLRA.2da Level 40 TDD and RoT
// SCRX41.itm ???
// baf and d files changes for TDDz (ee and non-ee)
// better graphics for tbc and pvrz
// repair pvrz
// spells
// kits
// biffing
// bggo
// are animation bams überprüfen, ob vorhanden


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : main component  	           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901000
	DESIGNATED 0 LABEL ~the_darkest_day~
	REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ ~~
	
	LAF ImportComponent STR_VAR aComp = ~CORE~ END

COPY_EXISTING ~sahoty01.cre~ ~override/tzhoty01.cre~
    WRITE_ASCII 0x280 ~tzhoty01~ #8

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Trollford		 	           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901001
	DESIGNATED 1
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~TROLLFORD~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Espurta		  			       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901002
	DESIGNATED 2
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~ESPURTA~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Purskal  					   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901003
	DESIGNATED 3
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~PURSKAL~ END
		
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Riatavin					   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901004
	DESIGNATED 4
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~RIATAVIN~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Onset of the Darkest Day	   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901005
	DESIGNATED 5
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~ONSETOfTheDARKESTDAY~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Darkest Day : Robilard					   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @901010
	DESIGNATED 6
	REQUIRE_PREDICATE MOD_IS_INSTALLED "TDD.tp2" (ID_OF_LABEL "TDD.tp2" "the_darkest_day" ) @901011

	LAF ImportComponent STR_VAR aComp = ~ROBILARD~ END

/////////////////////////////
BEGIN ~Use BGGO extended BG1 areas for TDD~
//REQUIRE_PREDICATE (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "2") OR (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "3") OR (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "4") "No TDD component detected."
//REQUIRE_COMPONENT "BGGO/SETUP-BGGO.TP2" "0" "The main component must be installed before"
REQUIRE_PREDICATE MOD_IS_INSTALLED "bggo.tp2" (ID_OF_LABEL "bggo.tp2" "baldurs_gate_graphical_overhaul" ) "No BGGO is installed"
REQUIRE_PREDICATE (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "2") OR (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "3") OR (MOD_IS_INSTALLED "TDD/SETUP-TDD.TP2" "4") "No TDD component detected."
REQUIRE_PREDICATE GAME_IS ~eet bgt~ ~Only for bgt or eet~

ACTION_IF GAME_IS ~eet~ BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_tis_tdd BEGIN
		"DD1900"    => "BG1900" //%BanditCamp%"
		"DD2300"    => "BG2300" //%FriendlyArmInn%"
		"DD3300"    => "BG3300" //%Beregost%"
		"DD8000"    => "BG2900" //%Larswood%"
	END
END

ACTION_IF GAME_IS ~bgt~ BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_tis_tdd BEGIN
		"DD1900"    => "AR8700" //%BanditCamp%"
		"DD2300"    => "AR6800" //%FriendlyArmInn%"
		"DD3300"    => "AR6700" //%Beregost%"
		"DD8000"    => "AR9000" //%Larswood%"
	END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN // turn off sleeping animals at night
		bird => 1
		eagle => 1
		vultur => 1
		seagul => 1
END

ACTION_PHP_EACH remapped_tis_tdd AS TDDARE => BG1ARE BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%TDDARE%.are~ BEGIN
		COPY_EXISTING ~%TDDARE%.are~ ~override~
		PATCH_IF SOURCE_SIZE > 0x11b BEGIN
			PATCH_IF (((BYTE_AT 0x48) << 7) & BIT7) BEGIN // Toggle only outdoor areas
				WRITE_BYTE 0x48 (THIS | BIT6 | BIT1 | BIT2) // Toggle extended night flag (BIT6) and day/night flag (BIT1) and weather flag (BIT2)
				GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110		// turn off creatures at night time
				PHP_EACH actors AS i => off BEGIN
					READ_ASCII off + 0x80 cre_resref
					TO_LOWER cre_resref
					PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
						WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
					END
				END
			END
			WRITE_ASCIIE 0x8 ~%BG1ARE%~ #8
		END
		BUT_ONLY
	END
END


//=============================================================================
/* USED DURING INITIAL DEVELOPMENT - NOT USED NOW*/
//BEGIN ~DEBUG~
//	NO_LOG_RECORD
//	DESIGNATED 99
//	INCLUDE ~%MOD_FOLDER%/Lib/Translations.tpa~
//		LAM DispatchTraFiles
