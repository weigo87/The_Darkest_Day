DEFINE_ACTION_MACRO InstallSpecificContents
BEGIN
	// TODO : BLOOD01X.cre-Barbazu better animation
	// TODO : BLOOD02X.cre-Amnizu wizard better animation	
	
	APPEND ~MASTAREA.2DA~
~ARPO05   value
ARPO25   value
DD1000   value
DD1x12   value
DD2300   value~
UNLESS ~ARPO05~

//	ACTION_IF GAME_IS ~eet bg2ee~ BEGIN
		COPY ~%RES_FOLDER%/SPL/SPSHAD2.spl~ ~override~
//	END
	
	COPY_EXISTING 	~EASTGOL1.cre~ ~override~
					~EASTGOL2.cre~ ~override~
		WRITE_LONG 0x28	0x7F04	// GOLEM_IRON 0xE250
	BUT_ONLY
	
	COPY_EXISTING 	~MINTSKEL.cre~ ~override~
		WRITE_ASCII 0x3C ~MINSKELL~	// portrait L
	BUT_ONLY
	
	COPY_EXISTING 	~DDMARY.cre~ ~override~
		WRITE_ASCII 0x3C ~RAVENM~	// portrait L
	BUT_ONLY
	
	COPY_EXISTING ~SUELLE2.cre~ ~override/DDSUELLE.cre~
		WRITE_ASCII DEATHVAR ~DDSUELLE~ #32
		WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
		WRITE_ASCII SCRIPT_CLASS ~~ #8
		WRITE_ASCII SCRIPT_RACE ~~ #8
		WRITE_ASCII SCRIPT_GENERAL ~~ #8
		WRITE_ASCII SCRIPT_DEFAULT ~~ #8
		WRITE_ASCII DIALOG ~DDSUELLE~ #8
	BUT_ONLY	
	
	LAM HandleSpellScrollsContract
	LAM HandleCoreItemsContract
	LAM HandleItemsContract
	
// EE Version
	LAM PatchStores

	LAM PatchArpo05
	LAM PatchArpo27
// end EE Version

	COPY_EXISTING ~ARPO05.are~ ~override~
		SAY 0x308 @20084
		SAY 0x3cc @20084
	
	EXTEND_BOTTOM ~AR0300.bcs~ ~%workspace%/%COMP_FOLDER%/Snip/bAR0300.baf~
	EXTEND_BOTTOM ~AR0902.bcs~ ~%workspace%/%COMP_FOLDER%/Snip/bAR0902.baf~
	EXTEND_BOTTOM ~AR0020.bcs~ ~%workspace%/%COMP_FOLDER%/Snip/bAR0020.baf~
	
	COPY_EXISTING ~DDTXT01.2DA~ ~override~
		REPLACE ~85747~ @20026
		REPLACE ~85748~ @20027
	
	OUTER_TEXT_SPRINT sender ~%COMP_FOLDER%~
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~ LAF Accept STR_VAR sender target = ~_ONSETOfTheDarkestDay~ END	
END


//=============================================================================
DEFINE_ACTION_MACRO HandleCoreItemsContract
BEGIN
	COPY ~%MOD_FOLDER%/_CORE/ItemsContract.2da~ ~%workspace%/%COMP_FOLDER%/CoreItemsContract.2da~
		COUNT_2DA_ROWS 7 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 7 resname				
			READ_2DA_ENTRY (count -1) 6 7 policy //undroppable column
			SET count = count -1
			
			TO_UPPER resname
			TO_UPPER policy
			SET customize = NOT (~%policy%~ STRING_EQUAL ~+~ || ~%policy%~ STRING_EQUAL ~++++++++~)
			SET undroppable = (~%policy%~ STRING_EQUAL ~.~)
			SET replace = (~%customize%~ && NOT ~%undroppable%~)
			SPRINT newItem ~%policy%~
			PATCH_IF (~%undroppable%~)
				BEGIN SPRINT newItem ~SCRL75~ END
			
			PATCH_IF (~%customize%~) BEGIN
				INNER_ACTION BEGIN
					ACTION_MATCH ~%resname%~
					WITH
						~DEATHREB~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ ~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = DEATHREB  new_item = EVAL "%newItem%" END END
						~MAGBOTXX~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ ~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = MAGBOTXX  new_item = EVAL "%newItem%" END END
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY	
END

//=============================================================================
DEFINE_ACTION_MACRO HandleItemsContract
BEGIN
	COPY ~%RES_FOLDER%/ItemsContract.2da~ ~%workspace%/%COMP_FOLDER%/ItemsContract.2da~
		COUNT_2DA_ROWS 7 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 7 resname
			READ_2DA_ENTRY (count -1) 6 7 policy //undroppable column
			SET count = count -1
			
			TO_UPPER resname
			TO_UPPER policy
			SET customize = NOT (~%policy%~ STRING_EQUAL ~+~ || ~%policy%~ STRING_EQUAL ~++++++++~)
			SET undroppable = (~%policy%~ STRING_EQUAL ~.~)
			SPRINT newItem ~%policy%~
			PATCH_IF (~%undroppable%~)
				BEGIN SPRINT newItem ~SCRL75~ END
						
			PATCH_IF (~%customize%~) BEGIN
				INNER_ACTION BEGIN					
					ACTION_MATCH ~%resname%~
					WITH
						~CLCKBS~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ 	~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = CLCKBS   new_item = EVAL "%newItem%" END END
						~FIGSILV~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/DDPOTEN1.d~ ~%workspace%/%COMP_FOLDER%/DDPOTEN1.d~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~MALICE~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ 	~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = MALICE   new_item = EVAL "%newItem%" END END
						~MOONLITE~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ LPF REPLACE_AREA_ITEM  STR_VAR old_item = MOONLITE new_item = EVAL "%newItem%" END END
						~POLAW4A~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/DDPOTEN1.d~ ~%workspace%/%COMP_FOLDER%/DDPOTEN1.d~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SLINGB2~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ 	~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = SLINGB2  new_item = EVAL "%newItem%" END END
						~SVIAN~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ LPF REPLACE_AREA_ITEM  STR_VAR old_item = SVIAN    new_item = EVAL "%newItem%" END END
						~TROLLARM~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ LPF REPLACE_AREA_ITEM  STR_VAR old_item = TROLLARM new_item = EVAL "%newItem%" END END
						~VIOLAX~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ 	~override~ LPF REPLACE_STORE_ITEM STR_VAR old_item = VIOLAX   new_item = EVAL "%newItem%" END END
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY	
END


//=============================================================================
DEFINE_ACTION_MACRO HandleSpellScrollsContract
BEGIN
	COPY ~%MOD_FOLDER%/_CORE/SpellScrollsContract.2da~ ~%workspace%/%COMP_FOLDER%/SpellScrollsContract.2da~
		COUNT_2DA_ROWS 8 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 8 resname				
			READ_2DA_ENTRY (count -1) 6 8 policy
			SET count = count -1
				
			TO_UPPER resname	
			TO_UPPER policy	
			SET customize = NOT (~%policy%~ STRING_EQUAL ~+~ || ~%policy%~ STRING_EQUAL ~++++++++~)	
			SET undroppable = (~%policy%~ STRING_EQUAL ~.~)
			SET replace = (~%customize%~ && NOT ~%undroppable%~)
			SPRINT newItem ~%policy%~
			PATCH_IF (~%undroppable%~)
				BEGIN SPRINT newItem ~SCRL75~ END
			
			PATCH_IF ~%customize%~ BEGIN
				INNER_ACTION BEGIN
					ACTION_MATCH ~%resname%~
					WITH	
						~SCRX1~ 	BEGIN COPY_EXISTING ~DDPOTEN1.sto~ ~override~ WRITE_ASCIIE 0x67C ~%newItem%~ #8 END
						~SCRX3~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO06.baf~ ~%workspace%/%COMP_FOLDER%/ARPO06.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX4~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO29.baf~ ~%workspace%/%COMP_FOLDER%/ARPO29.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX6~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX12~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX14~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX18~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO29.baf~ ~%workspace%/%COMP_FOLDER%/ARPO29.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX23~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX31~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ WRITE_ASCIIE 0xF48 	~%newItem%~ #8 END
						~SCRX32~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX39~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX40~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ WRITE_ASCIIE 0xF5C 	~%newItem%~ #8 END
						~SCRX54~ 	BEGIN COPY_EXISTING ~DD9400.are~ 	~override~ WRITE_ASCIIE 0xD04 	~%newItem%~ #8 END
						~SCRX60~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						~SCRX65~ 	BEGIN 
										COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ ~%workspace%/%COMP_FOLDER%/ARPO05.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~
										COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ 
									END
						~SCRX72~ 	BEGIN COPY_EXISTING ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ ~%workspace%/%COMP_FOLDER%/ARPO28.baf~ REPLACE_TEXTUALLY ~"%resname%"~ ~"%newItem%"~ END
						DEFAULT			
					END
				END
			END
		END
	BUT_ONLY	
END

//=============================================================================
DEFINE_ACTION_MACRO PatchStores BEGIN
	COPY_EXISTING ~Madeel.sto~ ~override~
		PATCH_FOR_EACH resname IN ~SCRX1~ ~SCRX2~ ~SCRX3~ ~SCRX4~ ~SCRX5~ ~SCRX6~ ~SCRX7~ ~SCRX9~ ~SCRX8~ ~SCRX10~ ~SCRX11~ ~SCRX12~ ~SCRX13~ ~SCRX14~ ~SCRX15~ ~SCRX16~ 
								~SCRX17~ ~SCRX18~ ~SCRX19~ ~SCRX20~ ~SCRX21~ ~SCRX22~ ~SCRX23~ ~SCRX24~ ~SCRX25~ ~SCRX26~ ~SCRX27~ ~SCRX28~ ~SCRX29~ ~SCRX31~ ~SCRX32~ 
								~SCRX33~ ~SCRX34~ ~SCRX35~ ~SCRX36~ ~SCRX37~ ~SCRX38~ ~SCRX39~ ~SCRX40~ ~SCRX41~ ~SCRX42~ ~SCRX43~ ~SCRX46~ ~SCRX47~ ~SCRX48~ ~SCRX49~ 
								~SCRX50~ ~SCRX51~ ~SCRX52~ ~SCRX53~ ~SCRX54~ ~SCRX55~ ~SCRX56~ ~SCRX57~ ~SCRX58~ ~SCRX59~ ~SCRX60~ ~SCRX61~ ~SCRX62~ ~SCRX63~ ~SCRX64~ 
								~SCRX65~ ~SCRX66~ ~SCRX67~ ~SCRX68~ ~SCRX69~ ~SCRX70~ ~SCRX71~ ~SCRX72~ ~SCRX73~ 
								~SCRM104~ ~FIGMIST~ ~FIGSENT~ ~FIGSILV~ ~RINGWWW~ ~DOVES~ ~BREACHAM~ ~AEGISRNG~ ~SW2H07~ ~SW2H09~ ~SW2H10~ ~PLAT16~ ~HELM20~ ~STAF05~ BEGIN
		REMOVE_STORE_ITEM ~%resname%~ END
END

//=============================================================================
DEFINE_ACTION_MACRO PatchArpo05	BEGIN
	COPY_EXISTING ~Arpo05.are~ ~override~
		READ_LONG 0x5c trigger_offset
		FOR( i = SHORT_AT 0x5a ; i ; --i )BEGIN
			READ_ASCII trigger_offset + (i - 1) * 0xc4 trigger_name (32) NULL
			READ_ASCII trigger_offset + (i - 1) * 0xc4 + 0x38 dest_name (32) NULL
			PATCH_IF ((~%trigger_name%~ STR_EQ ~ICM EXIT trigger~) AND (~%dest_name%~ STR_EQ ~ARPO08~)) BEGIN
				LPF fj_are_structure
				INT_VAR fj_delete_mode = i - 1 STR_VAR fj_structure_type = region END
			END
		END
END

//=============================================================================
DEFINE_ACTION_MACRO PatchArpo27 BEGIN
	COPY_EXISTING ~Arpo27.are~ ~override~ // fixes the east trigger point
		GET_OFFSET_ARRAY trigger_array ARE_V10_REGIONS 
		PHP_EACH trigger_array AS int => trigger_offset BEGIN
			READ_ASCII trigger_offset trigger_name (32) NULL
			READ_SHORT trigger_offset + 0x22 left_bounding
			PATCH_IF ((~%trigger_name%~ STR_EQ ~ICM EXIT trigger~) AND (left_bounding = 1193)) BEGIN
				WRITE_SHORT trigger_offset + 0x22 1103
			END
		END
END