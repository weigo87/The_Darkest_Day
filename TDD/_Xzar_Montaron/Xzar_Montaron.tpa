//=============================================================================
DEFINE_ACTION_MACRO InstallSpecificContents
BEGIN
	ACTION_IF !GAME_IS ~bgt~ BEGIN
		APPEND ~PDIALOG.2DA~ ~XZARDD XZARP *** *** *** *** *** ***~ UNLESS ~XZARDD~
		APPEND ~PDIALOG.2DA~ ~MONTYDD MONTAP *** *** *** *** *** ***~ UNLESS ~MONTYDD~
		COPY_EXISTING ~PDIALOG.2DA~ ~override~ PRETTY_PRINT_2DA
	END

	COPY_EXISTING ~AR0300.BCS~ ~override~
		REPLACE_BCS_BLOCK ~%workspace%/%COMP_FOLDER%/SNIP/oAR0300.BAF~ ~%workspace%/%COMP_FOLDER%/Snip/nAR0300.BAF~

	// BGT-WeiDU compatibility
	ACTION_IF GAME_IS ~bgt~ BEGIN    //BGT-WeiDU compatibility
		COPY_EXISTING ~BALDUR.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~"montydd"~  ~"Montaron"~  //change DeathVar check
				REPLACE_TEXTUALLY ~"xzardd"~   ~"Xzar"~      //change DeathVar check
			END
		COPY_EXISTING ~AR0300.BCS~ ~override~
			REPLACE_BCS_BLOCK ~%MOD_FOLDER%/Compat/BGT/Replace/rAR0300.BAF~ ~%MOD_FOLDER%/Compat/BGT/Replace/xAR0300.BAF~
		COPY_EXISTING ~MONTYRAS.BCS~   ~override~    //TDD scripts
					~DPLAYER2.BCS~   ~override~    //TDD scripts
					~HARPASS2.BCS~   ~override~    //TDD scripts
					~MONTAP.DLG~   ~override~  //TDD dialogue
					~XZARDD.DLG~   ~override~
					~XZARTHXB.DLG~   ~override~
 			DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY ~"xzardd"~  ~"Xzar"~        //change DeathVar check
			END
		COPY_EXISTING ~XZARDD.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY
~Global("tz_harperxzarfued","GLOBAL",0)~
~Global("tz_harperxzarfued","GLOBAL",0)
Global("ENDOFBG1","GLOBAL",2)~				
			END
		COPY_EXISTING ~ARAM00.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY
~ActionOverride("Xzar",LeaveParty())
ActionOverride("Xzar",ChangeAIScript("",DEFAULT))
ActionOverride("Xzar",ClearAllActions())
ActionOverride("Xzar",DestroySelf())~
~ActionOverride("Xzar",LeaveParty())
ActionOverride("Xzar",ChangeAIScript("",DEFAULT))
ActionOverride("Xzar",ClearAllActions())
SetGlobal("XzarPartyBG1","GLOBAL",1)
ActionOverride("Xzar",ChangeAIScript("SBTXZR",RACE))
ActionOverride("Xzar",MoveBetweenAreas("AR0300",[2372.2475],0))~
			END
	
		COPY + ~%MOD_FOLDER%/Compat/BGT/aXZARP.D~ ~%workspace%/%COMP_FOLDER%~
	END ELSE BEGIN
		COPY +	~%MOD_FOLDER%/Compat/TDD/XZARP.D~ ~%workspace%/%COMP_FOLDER%~
	END
	
		
	COPY_EXISTING ~MONTAR13.cre~ ~override~
		SAY 0x1cc @20094
	COPY_EXISTING ~XZAR13.cre~ ~override~
		SAY 0x1cc @20100


	OUTER_TEXT_SPRINT sender ~%COMP_FOLDER%~
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~ LAF Accept STR_VAR sender target = ~_Xzar_Montaron~ END
END
